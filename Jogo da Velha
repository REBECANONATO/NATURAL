************************************************************************
*  PROGRAMA.... RNSVELHA                                               *
*  OBJETIVO.... JOGO DA VELHA                                          *
*  AUTOR....... RNS                                                    *
*  DATA........ 04/2016                                                *
************************************************************************
DEFINE DATA LOCAL
*
1 #TL-CAMPO            (A01/3,3)
1 #TL-MSG              (A50)
1 #TL-P1               (A01)
1 #TL-P2               (A01)
*
1 #CV-CONTROL          (C)
1 #CV-CONTROL0         (C/3,3)
1 #INDI                (N01)
1 #INDJ                (N01)
1 #CONT                (I01)
1 #VEZ                 (I01)
1 #LINHA               (A09)
1 REDEFINE #LINHA
  2 #LINHA1            (A03)
  2 #LINHA2            (A03)
  2 #LINHA3            (A03)
1 #COLUNA              (A09)
1 REDEFINE #COLUNA
  2 #COLUNA1           (A03)
  2 #COLUNA2           (A03)
  2 #COLUNA3           (A03)
1 #DIAGONAL-P          (A03)
1 #DIAGONAL-S          (A03)
1 #VELHA               (A3/8)
1 #INDZ                (I01)
1 #INDY                (I01)
1 #VENCEU              (L)
*
END-DEFINE
*
RP01.
REPEAT
*
  INPUT WITH TEXT #TL-MSG USING MAP 'RNMVELHA'
  RESET #TL-MSG
*
  SET KEY ALL /* TIRAR DAQUI
* ############## TROCAR PARA DECIDE ####################################
  IF *PF-KEY EQ 'PF3'
    ESCAPE BOTTOM (RP01.)
  END-IF
*
  IF *PF-KEY EQ 'PF4' /* RESETAR VENCEU
    RESET #TL-P1 #CONT #TL-MSG #TL-P2 #CV-CONTROL
    RESET #TL-CAMPO(*,*) #CV-CONTROL0(*,*)
    REINPUT FULL 'NOVO JOGO'
  END-IF
* ################# CRIAR SUBROUTINE PARA VALIDAR #TL-P1 ##########
  IF #TL-P1 EQ ' '
    MOVE 'FAVOR DIGITAR "X" OU "O" PARA COMECAR O JOGO ' TO #TL-MSG
    REINPUT FULL 'VALOR INVALIDO'  MARK *#TL-P1 ALARM
  END-IF
*
  IF #TL-P1 NE 'X' AND #TL-P1 NE 'O'
    MOVE 'FAVOR DIGITAR "X" OU "O" PARA COMECAR O JOGO ' TO #TL-MSG
    REINPUT FULL 'VALOR INVALIDO' MARK *#TL-P1 ALARM
  END-IF
  MOVE (AD=P) TO #CV-CONTROL
*
  IF #TL-P1 EQ 'X'
    MOVE 'O' TO #TL-P2
  ELSE
    MOVE 'X' TO #TL-P2
  END-IF
*
* ############### CRIAR SUBROUTINE PARA O JOGO ####################
  IF #CONT < 10 /* IF CONT >=10 ESCAPE TOP
*
    RESET #TL-MSG
*
* /*####################CRIAR AUXILIAR #TL-P1 - #TL-P2 #################
*
    FR01. FOR #INDI 1 3
      FR02. FOR #INDJ 1 3
*
        IF #VEZ EQ 0
          IF #CV-CONTROL0(#INDI,#INDJ) MODIFIED
            IF #TL-CAMPO(#INDI,#INDJ) EQ #TL-P1
              MOVE (AD=P) TO #CV-CONTROL0(#INDI,#INDJ)
*
              PERFORM S-VENCEDOR
              IF #VENCEU
                ESCAPE BOTTOM (FR01.)
              END-IF
*
              MOVE 1 TO #VEZ
              ADD 1 TO #CONT
            ELSE
              COMPRESS 'FAVOR DIGITAR ' #TL-P1 ' NO CAMPO ' TO #TL-MSG
              REINPUT 'VALOR INVALIDO' MARK
                *#TL-CAMPO(#INDI,#INDJ) ALARM
            END-IF
          END-IF
        END-IF
*
        IF #VEZ EQ 1
          IF #CV-CONTROL0(#INDI,#INDJ) MODIFIED
            IF #TL-CAMPO(#INDI,#INDJ) EQ #TL-P2
              MOVE (AD=P) TO #CV-CONTROL0(#INDI,#INDJ)
*
              PERFORM S-VENCEDOR
              IF #VENCEU
                ESCAPE BOTTOM (FR01.)
              END-IF
*
              MOVE 0 TO #VEZ
              ADD 1 TO #CONT
            ELSE
              COMPRESS 'FAVOR DIGITAR ' #TL-P2 ' NO CAMPO ' TO #TL-MSG
              REINPUT 'VALOR INVALIDO' MARK
                *#TL-CAMPO(#INDI,#INDJ) ALARM
            END-IF
          END-IF
        END-IF
      END-FOR
    END-FOR
  END-IF
*
END-REPEAT /* RP01.
*
*
DEFINE SUBROUTINE S-VENCEDOR
* ----------------------------
*
COMPRESS #TL-CAMPO(1,1) #TL-CAMPO(1,2) #TL-CAMPO(1,3)
  INTO #LINHA1 LEAVING NO SPACE
*
COMPRESS #TL-CAMPO(2,1) #TL-CAMPO(2,2) #TL-CAMPO(2,3)
  INTO #LINHA2 LEAVING NO SPACE
*
COMPRESS #TL-CAMPO(3,1) #TL-CAMPO(3,2) #TL-CAMPO(3,3)
  INTO #LINHA3 LEAVING NO SPACE
*
COMPRESS #TL-CAMPO(1,1) #TL-CAMPO(2,2) #TL-CAMPO(3,3)
  INTO #DIAGONAL-P LEAVING NO SPACE
*
COMPRESS #TL-CAMPO(1,3) #TL-CAMPO(2,2) #TL-CAMPO(3,1)
  INTO #DIAGONAL-S LEAVING NO SPACE
*
COMPRESS #TL-CAMPO(1,1) #TL-CAMPO(2,1) #TL-CAMPO(3,1)
  INTO #COLUNA1 LEAVING NO SPACE
*
COMPRESS #TL-CAMPO(1,2) #TL-CAMPO(2,2) #TL-CAMPO(3,2)
  INTO #COLUNA2 LEAVING NO SPACE
*
COMPRESS #TL-CAMPO(1,3) #TL-CAMPO(2,3) #TL-CAMPO(3,3)
  INTO #COLUNA3 LEAVING NO SPACE
*
MOVE 1 TO #INDY
FOR #INDZ 1 3
  MOVE SUBSTRING(#COLUNA, #INDY, 3) TO #VELHA(#INDZ)
  ADD 3 TO #INDY
END-FOR
*
MOVE 1 TO #INDY
FOR #INDZ 4 6
  MOVE SUBSTRING(#LINHA, #INDY, 3) TO #VELHA(#INDZ)
  ADD 3 TO #INDY
END-FOR
*
MOVE 1 TO #INDY
MOVE SUBSTRING(#DIAGONAL-P, #INDY, 3) TO #VELHA(7)
MOVE 1 TO #INDY
MOVE SUBSTRING(#DIAGONAL-S, #INDY, 3) TO #VELHA(8)
*
*
RESET #INDY #INDZ
EXAMINE #VELHA(*) FOR 'XXX' GIVING #INDY
EXAMINE #VELHA(*) FOR 'OOO' GIVING #INDZ
*
DECIDE FOR FIRST CONDITION
  WHEN #INDY = 1 AND #TL-P1 = 'X'
    COMPRESS 'JOGADOR ' #TL-P1 ' VENCEU' TO #TL-MSG
    MOVE (AD=P) TO #CV-CONTROL0(*,*)
    MOVE TRUE TO #VENCEU
  WHEN #INDZ = 1 AND #TL-P1 = 'O'
    COMPRESS 'JOGADOR ' #TL-P1 ' VENCEU' TO #TL-MSG
    MOVE (AD=P) TO #CV-CONTROL0(*,*)
    MOVE TRUE TO #VENCEU
  WHEN #INDY = 1 AND #TL-P2 = 'X'
    COMPRESS 'JOGADOR ' #TL-P2 ' VENCEU' TO #TL-MSG
    MOVE (AD=P) TO #CV-CONTROL0(*,*)
    MOVE TRUE TO #VENCEU
  WHEN #INDZ = 1 AND #TL-P2 = 'O'
    COMPRESS 'JOGADOR ' #TL-P2 ' VENCEU' TO #TL-MSG
    MOVE (AD=P) TO #CV-CONTROL0(*,*)
    MOVE TRUE TO #VENCEU
  WHEN #CONT = 8
    MOVE 'DEU VELHA' TO #TL-MSG
  WHEN NONE
    IGNORE
END-DECIDE
*
END-SUBROUTINE
*

